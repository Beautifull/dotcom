#!/usr/local/bin/perl -w

########################################################################
# Weekly Shabbat Email form for sending candle lighting times and
# parsha information via email.
#
# Copyright (c) 2001  Michael J. Radwin.  All rights reserved.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
########################################################################

use strict;
use CGI;
use CGI::Carp qw(fatalsToBrowser);
use DB_File;
use Fcntl qw(:DEFAULT :flock);
use Hebcal;
use MIME::Base64;
use Email::Valid;

my($rcsrev) = '$Revision$'; #'

# process form params
my($q) = new CGI;

my($script_name) = $q->script_name();
$script_name =~ s,/index.html$,/,;

my($h1_text) = "1-Click Shabbat by Email (BETA)";
my($nav_text) = "Email";

my($cookies) = &Hebcal::get_cookies($q);
if (! $q->param('v'))
{
    my $want_c = 1;
    if (defined $cookies->{'E'})
    {
	my($cook) = new CGI($cookies->{'E'});
	if ($cook->param('em'))
	{
	    $q->param('em', &decode_base64($cook->param('em')));
	    my $info = &get_sub_info($q->param('em'));
	    if ($info)
	    {
		&Hebcal::process_cookie($q,$info);
		$want_c = 0;
	    }
	}
    }
    if (defined $cookies->{'C'} && $want_c)
    {
	&Hebcal::process_cookie($q,$cookies->{'C'});
    }
}

# sanitize input to prevent people from trying to hack the site.
# remove anthing other than word chars, white space, or hyphens.
my($key);
foreach $key ($q->param())
{
    next if $key eq 'em';
    my($val) = $q->param($key);
    $val = '' unless defined $val;
    $val =~ s/[^\w\s\.-]//g;
    $val =~ s/^\s*//g;		# nuke leading
    $val =~ s/\s*$//g;		# and trailing whitespace
    $q->param($key,$val);
}

if ($q->param('v'))
{
    my($email) = $q->param('em');
    if (!$email)
    {
	&form(1,
	      "Please enter your email address.");
    }
    elsif (! Email::Valid->address($email))
    {
	&form(1,
	      "Sorry, <b>" . &Hebcal::html_entify($email) . "</b> does\n" .
	      "not appear to be a valid email address.");
    }

    # email is OK, write canonicalized version
    $email = Email::Valid->address($email);
    $q->param('em', $email);
}
else
{
    &form(1);
}

if ($q->param('submit_modify'))
{
    &subscribe($q);
}
elsif ($q->param('submit_unsubscribe'))
{
    &unsubscribe($q);
}

close(STDOUT);
exit(0);

########################################################################

sub unsubscribe
{
    my($q) = @_;

    my $email = $q->param('em');
    my $info = &get_sub_info($email);

    if (!$info) {
	&form(1,
	      "Sorry, <b>" . &Hebcal::html_entify($email) . "</b> is\n" .
	      "not currently subscribed.");
    }

    my($now) = time;
    &write_sub_info($q, "action=UNSUBSCRIBE;t=$now");

    my($body) = qq{Hello,

Per your request, you have been removed from the weekly
Shabbat candle lighting time list.

Regards,
hebcal.com};

    &Hebcal::sendmail("shabbat-bounce\@hebcal.com",
		      "shabbat-owner\@hebcal.com",
		      "Hebcal Subscription Notification",
		      "You have been unsubscribed from hebcal",
		      '',$body,$email,'');

    my($html_email) = &Hebcal::html_entify($email);

    print STDOUT $q->header(),
    &Hebcal::start_html($q, 'Hebcal ' . $h1_text,
			undef, undef),
    &Hebcal::navbar2($q, $nav_text, 1,
		     "1-Click\nShabbat", "/shabbat/"),
    "<h1>$h1_text</h1>\n",
    qq{<h2>Unsubscribed</h2>
<p>You have been removed from the email subscription list.<br>
A confirmation message has been sent to <b>$html_email</b>.</p>
};
    print STDOUT &Hebcal::html_footer($q,$rcsrev);
}

sub subscribe
{
    my($q) = @_;

    my($zip) = $q->param('zip');

    if (!$zip)
    {
	&form(1,
	      "Please enter your zip code for candle lighting times.");
    }

    my($now) = time;
    my($email) = $q->param('em');
    
    if ($email =~ /\@hebcal.com$/)
    {
	&form(1,
	      "Sorry, can't use a <b>hebcal.com</b> email address.");
    }

    my($city_descr,$dst_descr,$tz_descr);
    $q->param('dst','usa')
	unless $q->param('dst');
    $q->param('tz','auto')
	unless $q->param('tz');
    $q->param('geo','zip');

    if ($q->param('zip') !~ /^\d{5}$/)
    {
	&form(1,
	      "Sorry, <b>" . $q->param('zip') . "</b> does\n" .
	      "not appear to be a 5-digit zip code.");
    }

    my($val) = &get_zip_info($q->param('zip'));

    &form(1,
	  "Sorry, can't find\n".  "<b>" . $q->param('zip') .
	  "</b> in the zip code database.\n",
	  "<ul><li>Please try a nearby zip code</li></ul>")
	unless defined $val;

    my($city,$state) = split(/\0/, substr($val,6));

    if (($state eq 'HI' || $state eq 'AZ') && $q->param('dst') eq 'usa')
    {
	$q->param('dst','none');
    }

    # handle timezone == "auto"
    my($tz) = &Hebcal::guess_timezone($q->param('tz'),
				      $q->param('zip'),
				      $state);
    unless (defined $tz)
    {
	&form(1, "Sorry, can't auto-detect\n" .
	      "timezone for <b>" . $city_descr . "</b>\n".
	      "(state <b>" . $state . "</b> spans multiple time zones).",
	      "<ul><li>Please select your time zone below.</li></ul>");
    }
    $q->param('tz', $tz);

    my(@city) = split(/([- ])/, $city);
    $city = '';
    foreach (@city)
    {
	$_ = lc($_);
	$_ = "\u$_";		# inital cap
	$city .= $_;
    }

    $city_descr = "$city, $state " . $q->param('zip');
    $dst_descr = "Daylight Saving Time: " . $q->param('dst');
    $tz_descr = "Time zone: " . $Hebcal::tz_names{$q->param('tz')};

    my($cookie_to_set);
    if (! defined $cookies->{'E'})
    {
	$cookie_to_set = sprintf("E=t=%d&em=%s", $now, &encode_base64($email));
    }
    else
    {
	my($newcookie) = sprintf("t=%d&em=%s", $now, &encode_base64($email));
	my($cmp1) = $newcookie;
	my($cmp2) = $cookies->{'E'};

	$cmp1 =~ s/^t=\d+\&//;
	$cmp2 =~ s/^t=\d+\&//;

	$cookie_to_set = "E=$newcookie"
	    if ($cmp2 ne 'opt_out' && $cmp1 ne $cmp2);
    }

    my($expires_date) = 'Thu, 15 Apr 2010 20:00:00 GMT';
    print STDOUT "Set-Cookie: ", $cookie_to_set,
    "; path=/email; expires=",  $expires_date, "\015\012"
	if $cookie_to_set;

    print STDOUT $q->header(),
    &Hebcal::start_html($q, 'Hebcal ' . $h1_text,
			undef, undef),
    &Hebcal::navbar2($q, $nav_text, 1,
		     "1-Click\nShabbat", "/shabbat/"),
    "<h1>$h1_text</h1>\n";

    # check if email address already verified
    my $info = &get_sub_info($email);
    if ($info)
    {
	&write_sub_info($q,
			sprintf("zip=%s;tz=%s;dst=%s;m=%s;t=%d",
				$q->param('zip'),
				$q->param('tz'),
				$q->param('dst'),
				$q->param('m'),
				$now));

	print STDOUT qq{<h2>Subscription Updated</h2>
<p>Your subsciption information has been updated successfully.</p>
<p><small>
$city_descr
<br>&nbsp;&nbsp;$dst_descr
<br>&nbsp;&nbsp;$tz_descr
</small></p>
};
    }
    else
    {
	my($encoded) = &write_staging_info($q,$now);

	my($body) = qq{Hello,

We have received your request to receive weekly Shabbat
candle lighting time information from hebcal.com for
$city_descr.

Please confirm your request by replying to this message.

If you did not request (or do not want) weekly Shabbat
candle lighting time information, please accept our
apologies and ignore this message.

Regards,
hebcal.com};

	my $replyto = "shabbat-subscribe+$encoded\@hebcal.com";
	my $status = 
	    &Hebcal::sendmail("shabbat-bounce\@hebcal.com",
			      $replyto,
			      "Hebcal Subscription Notification",
			      "Please confirm your request to subscribe to hebcal",
			      "Reply-To: $replyto",
			      $body,$email,'');

	my($html_email) = &Hebcal::html_entify($email);
	if ($status)
	{
	    print STDOUT qq{
<p>Thank you for your interest in weekly
candle lighting times and parsha information.</p>
<p>A confirmation message has been sent to <b>$html_email</b>.<br>
Simply reply to that message to confirm your subscription.</p>
<p><small>
$city_descr
<br>&nbsp;&nbsp;$dst_descr
<br>&nbsp;&nbsp;$tz_descr
</small></p>
};
	}
	else
	{
	    print STDOUT qq{<h2>Sorry!</h2>
<p>Unfortunately, we are temporarily unable to send email
to <b>$html_email</b>.</p>
<p>Please try again in a few minutes.</p>
<p>If the problem persists, please send email to
<a href="mailto:webmaster\@hebcal.com">webmaster\@hebcal.com</a>.</p>
};
	}
    }

    print STDOUT &Hebcal::html_footer($q,$rcsrev);
}

sub form
{
    my($head,$message,$help) = @_;

    if ($message ne '')
    {
	$help = '' unless defined $help;
	$message = "<hr noshade size=\"1\"><p><font\ncolor=\"#ff0000\">" .
	    $message . "</font></p>" . $help . "<hr noshade size=\"1\">";
    }

    if ($head) {
	print STDOUT $q->header(),
	&Hebcal::start_html($q, 'Hebcal ' . $h1_text,
			    undef, undef),
	&Hebcal::navbar2($q, $nav_text, 1,
			 "1-Click\nShabbat", "/shabbat/"),
	"<h1>$h1_text</h1>\n";
    }

    print STDOUT
	qq{$message\n},
	qq{<p>Subscribe to email weekly Shabbat candle lighting times.</p>\n},
	qq{<form name="f1" id="f1"\naction="$script_name" method="post">},
	qq{<label for="em">E-mail address:\n},
	$q->textfield(-name => 'em',
		      -id => 'em',
		      -size => 30),
	qq{</label><br>},
	qq{<label for="zip">Zip code:\n},
	$q->textfield(-name => 'zip',
		      -id => 'zip',
		      -size => 5,
		      -maxlength => 5),
	qq{</label>&nbsp;&nbsp;&nbsp;&nbsp;<label\nfor="tz">Time zone:\n},
	$q->popup_menu(-name => 'tz',
		       -id => 'tz',
		       -values => ['auto',-5,-6,-7,-8,-9,-10],
		       -default => 'auto',
		       -labels => \%Hebcal::tz_names),
	qq{</label><br>Daylight Saving Time:\n},
	$q->radio_group(-name => 'dst',
			-values => ['usa','none'],
			-default => 'usa',
			-labels =>
			{'usa' => "\nUSA (except AZ, HI, and IN) ",
			 'israel' => "\nIsrael ",
			 'none' => "\nnone ", }),
	$q->hidden(-name => 'geo',
		   -value => 'zip',
		   -override => 1),
	"<br><label\nfor=\"m1\">Havdalah minutes past sundown:\n",
	$q->textfield(-name => 'm',
		      -id => 'm1',
		      -size => 3,
		      -maxlength => 3,
		      -default => 72),
	"</label>",
	$q->hidden(-name => 'v',-value => 1,-override => 1),
	qq{\n<br>\n<input type="submit" name="submit_modify"\n},
	    qq{value="Subscribe">\n},
	qq{<input type="submit" name="submit_unsubscribe"\n},
	    qq{value="Unsubscribe">},
	qq{</form>};

    print STDOUT &Hebcal::html_footer($q,$rcsrev);

    exit(0);
}

sub get_zip_info
{
    my($zip) = @_;

    my($dbmfile) = 'zips.db';
    my(%DB);
    tie(%DB, 'DB_File', $dbmfile, O_RDONLY, 0444, $DB_File::DB_HASH)
	|| die "Can't tie $dbmfile: $!\n";

    my($val) = $DB{$zip};
    untie(%DB);

    $val;
}

sub get_sub_info
{
    my($em) = @_;

    my $dbmfile = 'subs.db';
    my(%DB);
    my($db) = tie(%DB, 'DB_File', $dbmfile, O_RDONLY, 0444,
		  $DB_File::DB_HASH)
	or die "$dbmfile: $!\n";

    my($fd) = $db->fd;
    open(DB_FH, "<&=$fd") || die "dup $!";
    unless (flock (DB_FH, LOCK_SH)) { die "flock: $!" }

    my $val = $DB{$em};

    flock(DB_FH, LOCK_UN);
    undef $db;
    undef $fd;
    untie(%DB);
    close(DB_FH);

    if ($val && $val =~ /^action=/) {
	return undef;
    }

    return $val;
}

sub write_staging_info
{
    my($q,$now) = @_;

    my $dbmfile = 'email.db';
    my(%DB);
    my($db) = tie(%DB, 'DB_File', $dbmfile, O_CREAT|O_RDWR, 0644,
		  $DB_File::DB_HASH)
	or die "$dbmfile: $!\n";

    my($fd) = $db->fd;
    open(DB_FH, "+<&=$fd") || die "dup $!";

    unless (flock (DB_FH, LOCK_EX)) { die "flock: $!" }

    my $rand = pack("V", int(rand(0xFFFFFFFF)));
    if ($ENV{'REMOTE_ADDR'}) {
	$rand .= pack("CCCC", split(/\./, $ENV{'REMOTE_ADDR'}));
    }
    $rand .= pack("V", $now);

    my $encoded = &encode_base64($rand);
    while (chomp($encoded)) { }

    # map potentially dangerous base64 chars to something better for email:
    #   + becomes . (dot)
    #   / becomes _ (underscore)
    #   = becomes - (hyphen)
    $encoded =~ s/\+/\./g;
    $encoded =~ s/\//_/g;
    $encoded =~ s/=/-/g;

    $DB{$encoded} = sprintf("zip=%s;tz=%s;dst=%s;m=%s;t=%d;em=%s",
			    $q->param('zip'),
			    $q->param('tz'),
			    $q->param('dst'),
			    $q->param('m'),
			    $now,
			    $q->param('em'));

    $db->sync;
    flock(DB_FH, LOCK_UN);
    undef $db;
    untie(%DB);
    close(DB_FH);

    $encoded;
}

sub write_sub_info
{
    my($q,$value) = @_;

    my $dbmfile = 'subs.db';
    my(%DB);
    my($db) = tie(%DB, 'DB_File', $dbmfile, O_CREAT|O_RDWR, 0644,
		  $DB_File::DB_HASH)
	or die "$dbmfile: $!\n";

    my($fd) = $db->fd;
    open(DB_FH, "+<&=$fd") || die "dup $!";

    unless (flock (DB_FH, LOCK_EX)) { die "flock: $!" }

    $DB{$q->param('em')} = $value;

    $db->sync;
    flock(DB_FH, LOCK_UN);
    undef $db;
    untie(%DB);
    close(DB_FH);
}

# local variables:
# mode: perl
# end:
